grammar com.github.funthomas424242.dsl.Ahnen with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate ahnen "http://www.github.com/funthomas424242/dsl/Ahnen"

Model:
	datei=Dateiart;

Dateiart:
	buch=Buch | familie=Familie;

Familie:
	"Familie" name=ID ";"
	(personen+=Person)+;

Person:
	{Person}
	"Person" name=STRING "{"
		"Vorname:" (vorname=STRING? | "unbekannt" ) "."
		"Nachname:" (nachname=STRING? | "unbekannt") "."
		"Geburtsname:" (geburtsname=STRING? | "unbekannt") "."
		"Mittelname:" (mittelname=STRING? | "unbekannt" | "-" ) "."
		"Geschlecht:" geschlecht=Geschlecht?  "."
		"Geburtsdatum:" (geburtsDatum=DATUM? | "unbekannt" ) "."
		"Todesdatum:" (todesDatum=DATUM? | "unbekannt" | "lebend" | "vermisst" )"."
		"Vater:" (vater=[Person|STRING]? | "unbekannt" | "unerfasst") "."
		"Mutter:" (mutter=[Person|STRING]? | "unbekannt" | "unerfasst") "."
		"Kinder:" (kinder=Kinder? | "keine") "."
		"Stiefkinder:" (stiefkinder=Kinder? | "keine") "."
		"Beziehungen:" "{"(beziehungen+=Beziehung+ | "keine") "}"
	"}";

Kinder:
	{Kinder}
	(kind=[Person|STRING] | "unbekannt" |"unerfasst") ("," next=Kinder)?
;

Beziehung:
	{Beziehung}
	"Beziehung:" "mit" (beziehung=[Person|STRING] | "unbekannt" | "unerfasst") "."
	"von:" (vonDatum=DATUM? | "unbekannt") "bis:" (bisDatum=DATUM? | "unbekannt" | "andauernd") "."
	"Hochzeit:" (hochzeitstag=DATUM? | "unbekannt" | "-") "." 
    "Scheidung:" (scheidungstag=DATUM? | "unbekannt" | "unabsehbar" | "demnächst" | "-") "."
	
;


enum Geschlecht:
	unbekannt='unbekannt'
 	| maennlich='männlich'
 	| weiblich='weiblich'
;

Buch:
	"Buch" name=STRING;

DATUM:
	DATE;

terminal DATE returns ecore::EDate:
	('0'..'9') ('0'..'9') '.' ('0'..'9') ('0'..'9') '.' ('0'..'9') ('0'..'9') ('0'..'9') ('0'..'9');
